apply plugin: 'com.android.application'

def appName = "JsBridge" // APP名称
def config = rootProject.ext.android // 配置

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName
        applicationId "com.henley.jsbridge.demo"
    }

    signingConfigs {

        henley {
            storeFile file('./henley.jks')
            storePassword '123456'
            keyAlias 'henley'
            keyPassword '123456'
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.henley
        }
        debug {
            debuggable true
            signingConfig signingConfigs.henley
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (!variant.buildType.debuggable) {
                output.assemble.doLast {
                    copy {
                        def targetName = "${appName}-v${variant.versionName}.apk"
                        from output.outputFile
                        into project.rootDir
                        rename { String fileName ->
                            fileName.replace("app-release.apk", targetName)
                        }
                        System.out.println("generate ${targetName} success in dir ${new File(project.rootDir, targetName)}.")
                    }
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
//    implementation project(':jsbridge')
    implementation 'com.henley.android:jsbridge:1.0.0'

}
